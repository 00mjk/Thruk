#!/bin/bash

# read rc file if exists
if [ -e ~/.thruk  ]; then
    . ~/.thruk
fi
if [ -e ~/.profile  ]; then
    . ~/.profile
fi

if [ ! -z $OMD_ROOT ]; then
    export PERL5LIB=$OMD_ROOT/share/thruk/lib:$PERL5LIB
    export CATALYST_CONFIG=$OMD_ROOT/etc/thruk
fi

eval 'exec /usr/bin/perl -x $0 ${1+"$@"} ;'
    if 0;

#! -*- perl -*-
# vim: expandtab:ts=4:sw=4:syntax=perl

use warnings;
use strict;
use File::Slurp;
use Thruk::Config;
use Thruk::Utils::CookieAuth;

my $prefix    = '';
my $urlprefix = "thruk";
if(defined $ENV{'OMD_SITE'}) {
    $prefix    = $ENV{'OMD_SITE'}.'/';
    $urlprefix = "[^/]+/thruk";
}

##########################################################
my $config              = Thruk::Config::get_config();
my $spath               = $config->{'tmp_path'}."/sessions";
my $loginurl            = $config->{'cookie_auth_login_url'}             || $prefix."thruk/cgi-bin/login.cgi";
my $sessiontimeout      = $config->{'cookie_auth_session_timeout'}       || 86400;
my $sessioncachetimeout = $config->{'cookie_auth_session_cache_timeout'} || 5;
my $sessioncache        = {};
$loginurl               = "/redirect/".$loginurl;

$| = 1;

##########################################################
while (<>) {
    chomp(my $in = $_);

    if($in =~ m|^/(.*?)/(.*?)/____/(.*)$|mx) {
        my $cookies = $1;
        my $remote  = $2;
        my $path    = $3;

        # some urls must pass
        if($path =~ m#^$urlprefix/(startup\.html|themes|javascript|cgi\-bin/(login|remote|restricted)\.cgi)#) {
            print "/pass/$path\n";
            next;
        }

        # did we get a cookie
        if($cookies eq '' or $cookies !~ m/thruk_auth=(\w+)/mx) {
            print $loginurl."?".$path."\n";
            next;
        }
        my $auth = $1;

        # use session cache for a few seconds
        my $now = time();
        if(defined $sessioncache->{$auth} and $sessioncache->{$auth}->{'time'} > $now - $sessioncachetimeout) {
            print "/loginok/", $sessioncache->{$auth}->{'login'} ,"/",$path,"\n";
            next;
        }

        # does our sessionfile exist?
        my $sessionfile = $spath.'/'.$auth;
        if(!-f $sessionfile) {
            print $loginurl."?".$path."\n";
            next;
        }

        # session timeout reached?
        my($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,
           $atime,$mtime,$ctime,$blksize,$blocks) = stat($sessionfile);
        if($mtime < $now - $sessiontimeout) {
            unlink($sessionfile);
            print $loginurl, "?", $path, "\n";
            next;
        }

        chomp(my $cont = read_file($sessionfile));
        my($basicauth, $ip, $user) = split(/~~~/mx, $cont, 3);

        # won't work in omds own apache mode
        #if($ip ne $remote) {
        #    print $loginurl, "?", $path, "\n";
        #    next;
        #}

        # validate session data
        if(!Thruk::Utils::CookieAuth::verify_basic_auth($config, $basicauth, $user)) {
            unlink($sessionfile);
            print $loginurl, "?", $path, "\n";
            next;
        }

        # touch session file and grant access
        utime(undef, undef, $sessionfile);
        $sessioncache->{$auth} = {
                'login' => $user,
                'time'  => $now,
        };
        print "/loginok/", $user, "/", $path, "\n";
        next;
    }

    # everything else redirects to login
    print $loginurl, "\n";
}
